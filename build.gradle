buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'maven'

group = 'jkmau5.modjam'
version = '0.1-SNAPSHOT'
archivesBaseName = 'RadioMod'

minecraft{
    version = '1.6.4-9.11.1.964'
    assetDir = 'runtime/client/assets'

    replace "@VERSION@", project.version
    replaceIn 'Constants.java'
}

jar {
    manifest {
        attributes 'Version': project.version
    }
    if (project.hasProperty("signmod")) {
        logger.info("Signing jar")
        doLast {
            ant.signjar(jar: jar.archivePath, alias: 'jkmau5', keystore: project.signmod.keystoreLocation, storepass: project.signmod.keystoreLocation)
        }
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'
        include '**/**.lang'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
        exclude '**/**.lang'
    }
}

uploadArchives {
    if (project.hasProperty("filesmaven")) {
        logger.info('Deploying to file server')
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "scp://maven.reening.nl/media/maven/") {
                authentication(userName: project.filesmaven.mavenUsername, password: project.filesmaven.mavenPassword)
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = 'javadoc'
    from 'build/docs/javadoc'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'dev'
}

artifacts {
    archives sourceJar
    archives deobfJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
